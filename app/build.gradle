apply plugin: 'com.android.application'
apply from: '../config/checkstyle.gradle'

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.3'
    defaultConfig {
        applicationId "swati4star.createpdf"
        vectorDrawables.useSupportLibrary = true
        minSdkVersion 21
        multiDexEnabled true
        targetSdkVersion 30
        versionCode 108
        versionName "8.8.1"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dexOptions {
        javaMaxHeapSize "4g"
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
    }
    lintOptions {
        checkReleaseBuilds false
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
    }
}
configurations {
    all {
        exclude module: 'httpclient'
    }
}
repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url "https://repository.aspose.com/repo/" }
}
dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'junit:junit:4.13.2'
    implementation 'androidx.vectordrawable:vectordrawable-animated:1.1.0'
    implementation 'androidx.exifinterface:exifinterface:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.legacy:legacy-support-v13:1.0.0'
    implementation 'com.google.android.material:material:1.4.0-beta01'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.0'

    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.preference:preference:1.1.1'

    // butterknife
    implementation 'com.jakewharton:butterknife:10.1.0'
    implementation 'androidx.test.ext:junit:1.1.3'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.1.0'

    // persistence room library - for db operations
    implementation 'androidx.room:room-runtime:2.3.0'
    annotationProcessor 'androidx.room:room-compiler:2.3.0'

    // material ripple, morphing button, material dialog, animations
    implementation 'com.balysv:material-ripple:1.0.2'
    implementation 'com.github.dmytrodanylyk:android-morphing-button:98a4986e56'
    implementation 'com.afollestad.material-dialogs:core:0.9.6.0'
    implementation 'com.airbnb.android:lottie:2.5.5'

    // libraries for reading from doc and docx files
    implementation group: 'org.apache.xmlbeans', name: 'xmlbeans', version: '2.4.0'
    implementation group: 'org.apache.poi', name: 'poi', version: '3.9'
    implementation group: 'org.apache.poi', name: 'poi-ooxml', version: '3.9'
    implementation group: 'org.apache.poi', name: 'poi-ooxml-schemas', version: '3.9'
    implementation group: 'org.apache.poi', name: 'poi-scratchpad', version: '3.9'

    // Itext pdf library
    implementation 'com.itextpdf:itextg:5.5.10'
    implementation 'com.madgag.spongycastle:core:1.58.0.0'

    // Picasso, image editor, image cropper
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'jp.wasabeef:picasso-transformations:2.2.1'
    implementation 'ja.burhanrashid52:photoeditor:0.2.1'
    implementation 'com.theartofdev.edmodo:android-image-cropper:2.7.0'

    // Viewpager transformation
    implementation 'com.eftimoff:android-viewpager-transformers:1.0.1@aar'

    // Image picker, and folder picker
    implementation 'com.zhihu.android:matisse:0.5.3-beta3'
    implementation 'lib.kashif:folderpicker:2.4'

    // Zxing - for scanning qr code
    implementation 'com.google.zxing:core:3.3.3'
    implementation 'com.journeyapps:zxing-android-embedded:3.4.0'

    // color picker
    implementation 'com.github.danielnilsson9:color-picker-view:1.4.0@aar'


    testImplementation 'org.mockito:mockito-core:2.23.4'
    testImplementation 'org.robolectric:robolectric:4.3.1'

    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}

preBuild.dependsOn('checkstyle')
assemble.dependsOn('lint')
check.dependsOn('checkstyle')
